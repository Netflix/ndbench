repositories {
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

dependencies {
    compile project(':ndbench-api')
    compile project(':ndbench-aws')
    compile "com.amazonaws:aws-java-sdk-dynamodb:latest.release"
    compile "com.amazonaws:aws-java-sdk-applicationautoscaling:latest.release"
    compile "com.amazonaws:aws-java-sdk-sts:latest.release"
    compile "com.amazonaws:aws-java-sdk-cloudwatch:latest.release"
    testCompile group: 'com.netflix.archaius', name: 'archaius2-guice', version: '2.1.11'
    testCompile 'com.netflix.governator:governator-test-junit:[1.15.3,)'
    testCompile project(':ndbench-core')
    testCompile 'com.amazonaws:DynamoDBLocal:[1.11,2.0)'

}

task copyNativeDeps(type: Copy) {
    from (configurations.testCompile) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/libs'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/libs'
    systemProperty "sqlite4java.library.path", 'build/libs'
}
